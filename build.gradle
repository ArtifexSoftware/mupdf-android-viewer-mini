apply plugin: 'com.android.application'

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:2.3.1'
	}
}

android {
	compileSdkVersion 16
	buildToolsVersion '25.0.3'

	def abiVersionOffsets = ['arm64-v8':1, 'armeabi':2, 'armeabi-v7a':3, 'mips':4, 'mips64':5, 'x86':6, 'x86_64':7]

	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 16
		ndk.abiFilters 'armeabi-v7a', 'x86'
		externalNativeBuild.ndkBuild.arguments '-j4'
		versionName '1.11 (git)'
		versionCode 11100
	}

	compileOptions {
		sourceCompatibility '1.7'
		targetCompatibility '1.7'
	}

	lintOptions {
		abortOnError false
	}

	tasks.withType(JavaCompile) {
		options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
	}

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs 'src', 'libmupdf/platform/java/src'
			res.srcDirs 'res'
		}
	}

	externalNativeBuild {
		ndkBuild.path 'libmupdf/platform/java/Android.mk'
	}

	splits {
		abi {
			enable true
			reset()
			include 'armeabi-v7a', 'x86'
			universalApk false
		}
	}

	if (project.hasProperty('RELEASE_KEY_STORE')) {
		signingConfigs {
			release {
				storeFile file(RELEASE_KEY_STORE)
				storePassword RELEASE_KEY_STORE_PASSWORD
				keyAlias RELEASE_KEY_ALIAS
				keyPassword RELEASE_KEY_ALIAS_PASSWORD
			}
		}

		buildTypes {
			release {
				signingConfig signingConfigs.release
			}
		}
	}

	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			def abiVersionOffset = abiVersionOffsets.get(output.getFilter(com.android.build.OutputFile.ABI))
			if (abiVersionOffset != null) {
				output.versionCodeOverride = variant.versionCode + abiVersionOffset
			}
		}
	}
}

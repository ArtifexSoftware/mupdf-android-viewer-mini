apply plugin: 'com.android.application'

group = 'com.artifex.mupdf'
version = '1.14.0'

dependencies {
	implementation 'com.android.support:appcompat-v7:25.+'
	if (file('../lib/build.gradle').isFile())
		api project(':lib')
	else
		api 'com.artifex.mupdf:mini:1.14.0'
}

android {
	compileSdkVersion 25
	defaultConfig {
		minSdkVersion 16
		targetSdkVersion 25
		versionName '1.14.0'
		versionCode 40
	}

	splits {
		abi {
			enable true
			universalApk true
		}
	}

	// Increment version code for each ABI variant
	def abiOffsets = [ 'armeabi-v7a':1, 'arm64-v8a':2, 'x86':3, 'x86_64':4, ]
	applicationVariants.all { variant ->
		variant.outputs.each { output ->
			def offset = abiOffsets.get(output.getFilter(com.android.build.OutputFile.ABI))
			if (offset != null)
				output.versionCodeOverride = variant.versionCode + offset
		}
	}

	if (project.hasProperty('release_storeFile')) {
		signingConfigs {
			release {
				storeFile file(release_storeFile)
				storePassword release_storePassword
				keyAlias release_keyAlias
				keyPassword release_keyPassword
			}
		}
		buildTypes {
			release {
				signingConfig signingConfigs.release
			}
		}
	}
}
